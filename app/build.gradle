def versionMajor = 0
def versionMinor = 8
def versionPatch = 3
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "one.mixin.messenger"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "x86", "arm64-v8a"
        }

        buildConfigField 'String', 'BUGSNAG_API_KEY', "\"${propOrEmpty('MIXIN_BUGSNAG_API_KEY')}\""
        buildConfigField 'String', 'RECAPTCHA_KEY', "\"${propOrEmpty('MIXIN_RECAPTCHA_KEY')}\""
        buildConfigField 'String', 'GIPHY_KEY', "\"${propOrEmpty('MIXIN_GIPHY_KEY')}\""

        packagingOptions {
            exclude 'kotlin/**'
            exclude '**/*.kotlin_metadata'
            exclude 'META-INF/*.kotlin_module'
            exclude 'META-INF/*.version'
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }

        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    productFlavors {
    }

    signingConfigs {
        debug {
            storeFile file('keys/debug.keystore')
            storePassword 'android'
            keyAlias 'AndroidDebugKey'
            keyPassword 'android'
        }
        if (project.hasProperty("RELEASE_STORE_FILE")) {
            release {
                keyAlias "${RELEASE_KEY_ALIAS}"
                keyPassword "${RELEASE_KEY_PASSWORD}"
                storeFile file("${RELEASE_STORE_FILE}")
                storePassword "${RELEASE_STORE_PASSWORD}"
            }
        }
    }

    buildTypes {
        if (project.hasProperty("RELEASE_STORE_FILE")) {
            release {
                debuggable false
                jniDebuggable false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
        debug {
            signingConfig signingConfigs.debug
            jniDebuggable false
        }
    }

    lintOptions {
        abortOnError false
    }
    configurations {
        ktlint
    }

    configurations.all {
        resolutionStrategy.force "com.android.tools.build.jetifier:jetifier-core:1.0.0-beta02"
    }
}

dependencies {
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:${appcompatVersion}"
    implementation "androidx.legacy:legacy-support-v4:${supportLibVersion}"
    implementation "com.google.android.material:material:${supportLibVersion}"
    implementation "androidx.cardview:cardview:${supportLibVersion}"
    implementation "androidx.recyclerview:recyclerview:${supportLibVersion}"
    implementation "androidx.exifinterface:exifinterface:${supportLibVersion}"
    implementation "androidx.browser:browser:${supportLibVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${constraintLayoutVersion}"
    implementation "androidx.core:core-ktx:${ktxVersion}"

    implementation "android.arch.work:work-runtime-ktx:${workManagerVersion}"

    // Architecture components
    implementation "androidx.paging:paging-runtime:${pagingVersion}"
    implementation "androidx.lifecycle:lifecycle-extensions:${lifecycleVersion}"
    kapt "androidx.lifecycle:lifecycle-compiler:${lifecycleVersion}"
    implementation "androidx.room:room-runtime:${roomVersion}"
    kapt "androidx.room:room-compiler:${roomVersion}"
    implementation "androidx.room:room-rxjava2:${roomVersion}"
    implementation "android.arch.navigation:navigation-fragment-ktx:${navigationVersion}"
    implementation "android.arch.navigation:navigation-ui-ktx:${navigationVersion}"

    // DI
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "com.google.dagger:dagger-android:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"
    kapt "com.google.dagger:dagger-android-processor:${daggerVersion}"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
    implementation("io.jsonwebtoken:jjwt:${jwtVersion}")
    implementation "com.jakewharton.threetenabp:threetenabp:${threetenabpVersion}"
    implementation "com.bugsnag:bugsnag-android:${bugsnagVersion}"
    implementation "org.jsoup:jsoup:${jsoupVersion}"

    // retrofit
    implementation "com.github.seniorzhai.okhttp:okhttp:3.11.2"
    implementation("com.squareup.retrofit2:retrofit:${retrofitVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation("com.squareup.retrofit2:converter-gson:${retrofitVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation("com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation("com.squareup.okhttp3:logging-interceptor:${okhttpVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.google.firebase:firebase-core:${firebaseVersion}"
    implementation "com.google.firebase:firebase-messaging:${firebaseMessageVersion}"
    implementation "com.google.firebase:firebase-dynamic-links:${firebaseDylinkVersion}"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.6'
    implementation "com.google.zxing:core:${zxingVersion}"
    implementation "com.timehop.stickyheadersrecyclerview:library:${stickyheadersrecyclerviewVersion}"
    implementation "org.whispersystems:signal-protocol-android:${signalVersion}"
    implementation 'org.whispersystems:curve25519-android:0.5.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk25:$anko_version"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "com.zlc.glide:webpdecoder:$webpdecoderVesion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation("com.github.bumptech.glide:okhttp3-integration:$glideVersion") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "jp.wasabeef:glide-transformations:${glideTransformationsVersion}"
    implementation "com.jakewharton.timber:timber:${timberVersion}"
    implementation "com.facebook.stetho:stetho:${stethoVersion}"
    implementation("com.facebook.stetho:stetho-okhttp3:${stethoVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation "com.github.jorgecastilloprz:fabprogresscircle:${fabprogresscircleVersion}"
    implementation "com.github.tougee:country-picker-android:${countryPickerVersion}"
    implementation "com.dmitrymalkovich.android:progress-fab:${fabVersion}"
    implementation "com.googlecode.libphonenumber:libphonenumber:${libphonenumberVersion}"
    implementation "com.github.tougee:rx-contacts2:${rxContactsVersion}"
    implementation "com.github.tougee:zxing-android-embedded:${zxingEmbeddedVersion}"
    implementation "com.github.tougee:android-priority-jobqueue:${jobqueueVersion}"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:${rxpermissionsVersion}"
    implementation("com.github.yalantis:ucrop:${ucropVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation "com.facebook.rebound:rebound:${reboundVersion}"
    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    ktlint "com.github.shyiko:ktlint:${ktlintVersion}"
    testImplementation "junit:junit:${junitVersion}"
    androidTestImplementation "junit:junit:${junitVersion}"
    androidTestImplementation("androidx.test.espresso:espresso-core:${espressoVersion}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.uber.autodispose:autodispose-kotlin:${autodisposeVersion}"
    implementation "com.uber.autodispose:autodispose-android-archcomponents-kotlin:${autodisposeVersion}"
    implementation "com.google.android.exoplayer:exoplayer-core:${exoplayerVersion}"
    implementation("com.google.android.exoplayer:extension-okhttp:${exoplayerVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation "com.googlecode.mp4parser:isoparser:${isoparserVersion}"
    implementation "com.github.SeniorZhai:LargeImage:${largeImageViewVersion}"
    implementation "com.github.tougee:BiometricPromptCompat:${biometricpromptVersion}"
    implementation "org.webrtc:google-webrtc:${webrtcVersion}"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args '--android', "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args '--android', "-F", "src/**/*.kt"
}

// preBuild.dependsOn ktlint

androidExtensions {
    experimental = true
}

apply plugin: 'com.google.gms.google-services'
